// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //use some registers, need a few multiplexors plus and incrementer, make sure to loop
	//check if inc
	Inc16(in=feedback, out=temp1);
	Mux16(a=feedback, b=temp1, sel=inc, out=part1);
	//load
	Mux16(a=part1, b=in, sel=load, out=part2);
	//check if reset
	Mux16(a=part2, b=false, sel=reset, out=part3);
	//always drop in a the final value
	Register(in=part3, load=true, out=out, out=feedback);
}
